        PROGRAM ::= CONFIGUREARENA? SETPLAYERPOS (STATEMENT | LOOPSTATEMENT)+
 CONFIGUREARENA ::= "arena size" DIGITTUPLE
     DIGITTUPLE ::= DIGIT "," DIGIT // e.g. width, height / xpos, ypos
   SETPLAYERPOS ::= SETPLAYERX SETPLAYERY
     SETPLAYERX ::= "set xpos of player to" VALUE
     SETPLAYERY ::= "set ypos of player to" VALUE
      STATEMENT ::= MAKESTATEMENT | MODIFYSTATMENT
  MAKESTATEMENT ::= "make a" BLOCKTYPE "called" BLOCKNAME ("with size" DIGITTUPLE)? "at" DIGITTUPLE
MODIFYSTATEMENT ::= REMOVESTATEMENT | SETSTATEMENT | CHANGESTATEMENT | MOVESTATEMENT
REMOVESTATEMENT ::= "destroy" BLOCKNAME
   SETSTATEMENT ::= "set" FIELD "of" OBJECT "to" VALUE
CHANGESTATEMENT ::= "change" NUMBERFIELD "of" OBJECT "by" DIGIT
  MOVESTATEMENT ::= "move" BLOCKNAME DIR "by" DIGIT
  LOOPSTATEMENT ::= "do every" DIGIT TIME ":" (STATEMENT | WAITSTATEMENT)+ "end loop"
  WAITSTATEMENT ::= "wait" DIGIT TIME
           TIME ::= "ms"
      BLOCKTYPE ::= "block" | "goal"
         OBJECT ::= "player" | BLOCKNAME
      BLOCKNAME ::= STRING
            DIR ::= "up" | "down" | "left" | "right"
          FIELD ::= NUMBERFIELD | "colour"
    NUMBERFIELD ::= POSITION | DIMENSION
       POSITION ::= "xpos" | "ypos"
      DIMENSION ::= "width" | "height"
          VALUE ::= DIGIT | COLOUR
         COLOUR ::= "#" DIGIT // Hex colour code
          DIGIT ::= (-)?[0-9]+
         STRING ::= [A-Za-z0-9]+