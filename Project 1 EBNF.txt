        PROGRAM ::= CONFIGUREARENA? (STATEMENT | LOOP)+
 CONFIGUREARENA ::= "arena size" DIGITTUPLE
     DIGITTUPLE ::= DIGIT "," DIGIT // e.g. width, height / xpos, ypos
      STATEMENT ::= MAKESTATEMENT | REMOVESTATEMENT | SETSTATEMENT | ACTIONSTATEMENT | WAITSTATEMENT
  MAKESTATEMENT ::= "make a" BLOCKTYPE "called" BLOCKNAME ("with size" SIZE)? "at" DIGITTUPLE
REMOVESTATEMENT ::= "destroy" BLOCKNAME
   SETSTATEMENT ::= "set" FIELD "of" OBJECT "to" VALUE | "change" FIELD "of" OBJECT "by" DIGIT
ACTIONSTATEMENT ::= "move" BLOCKNAME DIR "by" DIGIT
  WAITSTATEMENT ::= "wait" DIGIT TIME
           LOOP ::= "do every" DIGIT TIME ":" STATEMENT+ "end loop"
           TIME ::= "ms"
      BLOCKTYPE ::= "block" | "goal"
         OBJECT ::= "player" | BLOCKNAME
      BLOCKNAME ::= STRING
            DIR ::= "up" | "down" | "left" | "right"
          FIELD ::= POSITION | DIMENSION | "colour"
       POSITION ::= "xpos" | "ypos"
      DIMENSION ::= "width" | "height"
          VALUE ::= DIGIT | COLOUR
         COLOUR ::= "#" DIGIT // Hex colour code
          DIGIT ::= (-)?[0-9]+
         STRING ::= [A-Za-z0-9]+